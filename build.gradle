defaultTasks 'distribution'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: "android-library"
android {
    compileSdkVersion 18
    buildToolsVersion '18.0.1'
    defaultConfig {
        versionCode = 1
        versionName = version
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
        }
        instrumentTest.setRoot('src/instrumentTest')
    }
}


repositories {
    mavenCentral()
}

sourceSets {
        unitTest {
                java.srcDir file('src/test')
        }
}

dependencies {
        unitTestCompile files("$project.buildDir/classes/debug")
        unitTestCompile 'junit:junit:[4,)'
        unitTestCompile 'org.robolectric:robolectric:2.1.1'
		unitTestCompile 'com.google.android:android:4.0.1.2'        
}

configurations {
        unitTestCompile.extendsFrom runtime
        unitTestRuntime.extendsFrom unitTestCompile
}

task unitTest(type:Test, dependsOn: assemble) {
        description = "run unit tests"
        testClassesDir = project.sourceSets.unitTest.output.classesDir
        classpath = project.sourceSets.unitTest.runtimeClasspath
}

unitTest {
	testLogging {
		exceptionFormat = 'full'
	}
}

task bundleEclipse << {
    exec {
        executable 'scripts/bundle-eclipse'
    }
}


task wrapper(type: org.gradle.api.tasks.wrapper.Wrapper) {
    gradleVersion = '1.4'
}

task distribution(dependsOn: ["bundleEclipse", "build", "clean"]) << {
    println "Building with version=$version"
}

task release(dependsOn: 'distribution') << {
	println('Doing release build')
}

gradle.taskGraph.whenReady {taskGraph ->
    if (!taskGraph.hasTask(release)) {
        version = version+'-SNAPSHOT'
    }
}

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    try {
      exec {
          commandLine 'git', 'describe', '--tags'
          standardOutput = stdout
      }
      return stdout.toString().trim()
    }
    catch (e) {
      println("Can't get version from git: "+e);
      return "adhoc"
    }
}

version = getVersionName()

build.mustRunAfter clean
bundleEclipse.mustRunAfter build


